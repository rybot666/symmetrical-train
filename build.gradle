plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'io.github.rybot666'
version = '0.1'

repositories {
    mavenCentral()
    mavenLocal()

    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }

    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }

    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    shadow 'org.spongepowered:mixin:0.8.5'
    shadow 'net.bytebuddy:byte-buddy-agent:1.12.10'
    shadow 'org.ow2.asm:asm:9.3'
    shadow 'org.ow2.asm:asm-tree:9.3'
    shadow 'org.ow2.asm:asm-util:9.3'
    shadow 'org.ow2.asm:asm-commons:9.3'

    compileOnly 'org.spigotmc:spigot:1.18.2-R0.1-SNAPSHOT'

    implementation 'org.jetbrains:annotations:23.0.0'
}

def targetJavaVersion = 9
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = 1.9
    targetCompatibility = 1.9
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props

    filteringCharset 'UTF-8'

    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    configurations = [project.configurations.shadow]

    relocate 'org.objectweb.asm', 'io.github.rybot666.libs.org.objectweb.asm'
    relocate 'net.bytebuddy', 'io.github.rybot666.libs.net.bytebuddy'
}

assemble.dependsOn shadowJar
